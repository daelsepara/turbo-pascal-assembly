# Test for presence and identify 8087/80287/80387 FPU

This routine, called durining initialization, will attempt to detect and identify the system's **FPU**.

```nasm
SYS074C: MOV BX,FPU.StatusWord
```

Get pointer to Status Word of **[FPU Data](DATA8087.md)**.

```nasm
SYS074F: XOR DI,DI
SYS0751: MOV ES,[PrefixSeg]
SYS0755: ES:
SYS0756: MOV ES,[PrefixSeg:002C]
```

Set **ES**:**DI** to the address of DOS environment in the **[PrefixSeg:002C](../DATA.md)**.

```nasm
SYS075A: MOV CX,7FFF
SYS075D: CLD
```

Prepare to search the entire segment (64K bytes ~ 32K words).

```nasm
SYS075E: ES:
SYS075F: MOV AX,[DI]
SYS0761: OR AL,AL
SYS0763: JZ 0780
```

Check if we have reached the end of the environment string.

```nasm
SYS0765: CMP AX,3738
```

Check if the characters in **ES**:**DI** are '**87**'.

```nasm
SYS0768: JNZ 077A
SYS076A: ES:
SYS076B: MOV AX,[DI+02]
SYS076E: CMP AL,3D
SYS0770: JNZ 077A
```

Check if the next character is '**=**' (3Dh).

```nasm
SYS0772: AND AH,DF
SYS0775: CMP AH,59
SYS0778: JMP 079D
```

Check if the environment string contains the cahracters '**87=Y**'.

```nasm
SYS077A: XOR AX,AX
SYS077C: REPNZ
SYS077D: SCASB
SYS077E: JZ 075E
```

Continue searching through the environment variables.

```nasm
SYS0780: XOR AX,AX
```

```nasm
SYS0782: PUSH SP
SYS0783: POP DX
SYS0784: CMP DX,SP
SYS0786: JNZ 078A
```

Check stack?

```nasm
SYS0788: OUT F0,AL
```

Clear FPU Busy Latch (Port **[F0](PORTS.md)**) by writing 00h to it.

```nasm
SYS078A: FINIT
SYS078C: MOV [BX],AX
```

Intializes:
- **[[FPU.StatusWord]](DATA8087.md)** (address in **BX**) = **AX** = 0.
- FPU after checking for pending unmasked floating-point exceptions
  - **[Control Word](CONTROL8087.md)** = 037FH (round to nearest, all exceptions masked, 64-bit precision)
  - **[Status Word](STATUS8087.md)** - 0000H (no exception flags set, Stack Top Pointer = 000 Top)

```nasm
SYS078E: FSTCW [BX]
```

Get Control Word and Store in [**BX**].

```nasm
SYS0790: MOV CX,0014
SYS0793: LOOP 0793
```

Delay?

```nasm
SYS0795: MOV AX,[BX]
SYS0797: AND AX,0F3F
SYS079A: CMP AX,033F
```

Retrieve Control Word from [**BX**] then check if: 
- = 0011 0011 1111 = 033F
  - Rounding is even
  - 64-bit precision
  - All exceptions masked

```nasm
SYS079D: MOV DX,1330
SYS07A0: MOV AL,00
```

Set defaults:
- **AL** = 0 (No FPU)
- **[Control Word](CONTROL8087.md)**:
  - 1330 = 0001 0011 0011 0000:
    -  1 Infinity Control Set
    - 00 round to nearest even
    - 11 64 bit precision
    - Masked exceptions
      - Precision
      - Underflow

```nasm
SYS07A2: JNZ 07D1
```

Exit if **[Control Word](CONTROL8087.md)** does not match expected setting, i.e. No FPU.

```nasm
SYS07A4: PUSH SP
SYS07A5: POP AX
SYS07A6: CMP AX,SP
SYS07A8: MOV AL,01
SYS07AA: JNZ 07D1
```

If stack behavior is different then:
- **AL** = 01 (8087)

```nasm
SYS07AC: WAIT
SYS07AD: FINIT
```

Initialize FPU again.

```nasm
SYS07AF: WAIT
SYS07B0: FLD1
```

Load **1.0** onto the stack.

```nasm
SYS07B2: WAIT
SYS07B3: FLDZ
```

Load **0.0** onto the stack.

```nasm
SYS07B5: WAIT
SYS07B6: FDIVP ST(1),ST
```

Divide **ST(1)** = **ST(1)** / **ST** = **1.0** / **0.0**, then pop off the stack.

```nasm
SYS07B8: WAIT
SYS07B9: FLD ST(0)
```

Push **ST(0)** onto the stack.

```nasm
SYS07BB: WAIT
SYS07BC: FCHS
```

Change the sign of **ST(0)**

```nasm
SYS07BE: WAIT
SYS07BF: FCOMPP ST(1)
```

Compare **ST(0)** and **ST(1)**.

```nasm
SYS07C1: WAIT
SYS07C2: FSTSW [BX]
```

Store **[Status Word](STATUS8087.md)** in [**BX**] (**[FPU.StatusWord]**).

```nasm
SYS07C4: WAIT
SYS07C5: MOV AX,[BX]
```

Copy **[Status Word](STATUS8087.md)** to **AX**.

```nasm
SYS07C7: SAHF
```

Store **AH** to **Flags**. At this point **AH** contains the upper-8 bits of the **[Status Word](STATUS8087.md)**:

|Bit|Name|Description      |Flag |
|---|----|-----------------| :-: |
|07 |B   |Busy             | SF  |
|06 |C3  |Condition Code 3 | ZF  |
|05 |ST  |Stack Top Pointer|  0  |
|04 |    |Stack Top Pointer| AF  |
|03 |    |Stack Top Pointer|  0  |
|02 |C2  |Condition Code 2 | PF  |
|01 |C1  |Condition Code 1 |  1  |
|00 |C0  |Condition Code 0 | CF  |

**SAHF** transfers its value into the Flags register using the mapping above.

```nasm
SYS07C8: MOV AL,02
SYS07CA: JZ 07D1
```

If **ZF** is set (= 1) then FPU is a **80287** (**AL** = 02) because it does not distinguish between a postive infinity and a negative infinity.

```nasm
SYS07CC: MOV DX,1332
SYS07CF: MOV AL,03
```

Set:
- **AL** = 03 **FPU** is **80387**
- **[Control Word](CONTROL8087.md)**:
  - 1332 = 0001 0011 0011 0010:
    -  1 Infinity Control Set
    - 00 Round to nearest even
    - 11 64 bit precision
    - Masked exceptions
      - Precision
      - Underflow
      - Denormalized Operand

```nasm
SYS07D1: MOV [Test8087],AL
SYS07D4: MOV [FPU.ControlWord],DX
SYS07D8: RET
```

Set **[FPU type](../DATA.md)** and **[Control Word](DATA8087.md)** and return.

Because it returns with a **NEAR RET**, it can only be called from within the system library.

See also: [Test8087 (Variable)](../DATA.md), [8087/80287/80387 Control Word](CONTROL8087.md), [8087/80287/80387 Status Word](STATUS8087.md), [8087/80287/80387 x87 FPU Evironment](ENV8087.md), [8087/80287/80387 Temporary data](DATA8087.md), or go [back](../../README.md)
