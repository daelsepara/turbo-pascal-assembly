# Test for presence and identify 8087/80287/80387 FPU

This routine, called durining initialization, will attempt to detect and identify the system's **FPU**.

```nasm
SYS:074C BBA402        MOV	BX,FPU.StatusWord
```

Get pointer to Status Word of **[FPU Data](DATA8087.md)**.

```nasm
SYS:074F 33FF          XOR	DI,DI
SYS:0751 8E063800      MOV	ES,[PrefixSeg]
SYS:0755 26            ES:
SYS:0756 8E062C00      MOV	ES,[PrefixSeg:002C]
```

Set **ES**:**DI** to the address of DOS environment in the **[PrefixSeg:002C](../DATA.md)**.

```nasm
SYS:075A B9FF7F        MOV	CX,7FFF
SYS:075D FC            CLD
```

Prepare to search the entire segment (64K bytes ~ 32K words).

```nasm
SYS:075E 26            ES:
SYS:075F 8B05          MOV	AX,[DI]
SYS:0761 0AC0          OR	AL,AL
SYS:0763 741B          JZ	0780
```

Check if we have reached the end of the environment string.

```nasm
SYS:0765 3D3837        CMP	AX,3738
```

Check if the characters in **ES**:**DI** are '**87**'.

```nasm
SYS:0768 7510          JNZ	077A
SYS:076A 26            ES:
SYS:076B 8B4502        MOV	AX,[DI+02]
SYS:076E 3C3D          CMP	AL,3D
SYS:0770 7508          JNZ	077A
```

Check if the next character is '**=**' (3Dh).

```nasm
SYS:0772 80E4DF        AND	AH,DF
SYS:0775 80FC59        CMP	AH,59
SYS:0778 EB23          JMP	079D
```

Check if the environment string contains the cahracters '**87=Y**'.

```nasm
SYS:077A 33C0          XOR	AX,AX
SYS:077C F2            REPNZ
SYS:077D AE            SCASB
SYS:077E 74DE          JZ	075E
```

Continue searching through the environment variables.

```nasm
SYS:0780 33C0          XOR	AX,AX
```

```nasm
SYS:0782 54            PUSH	SP
SYS:0783 5A            POP	DX
SYS:0784 3BD4          CMP	DX,SP
SYS:0786 7502          JNZ	078A
```

Check stack?

```nasm
SYS:0788 E6F0          OUT	F0,AL
```

Clear FPU Busy Latch (Port **[F0](PORTS.md)**) by writing 00h to it.

```nasm
SYS:078A DBE3          FINIT
SYS:078C 8907          MOV	[BX],AX
```

Intializes:
- **[[FPU.StatusWord]](DATA8087.md)** (address in **BX**) = **AX** = 0.
- FPU after checking for pending unmasked floating-point exceptions
  - **[Control Word](CONTROL8087.md)** = 037FH (round to nearest, all exceptions masked, 64-bit precision)
  - **[Status Word](STATUS8087.md)** - 0000H (no exception flags set, Stack Top Pointer = 000 Top)

```nasm
SYS:078E D93F          FSTCW	[BX]
```

Get Control Word and Store in [**BX**].

```nasm
SYS:0790 B91400        MOV	CX,0014
SYS:0793 E2FE          LOOP	0793
```

Delay?

```nasm
SYS:0795 8B07          MOV	AX,[BX]
SYS:0797 253F0F        AND	AX,0F3F
SYS:079A 3D3F03        CMP	AX,033F
```

Retrieve Control Word from [**BX**] then check if: 
- = 0011 0011 1111 = 033F
  - Rounding is even
  - 64-bit precision
  - All exceptions masked

```nasm
SYS:079D BA3013        MOV	DX,1330
SYS:07A0 B000          MOV	AL,00
```

Set defaults:
- **AL** = 0 (No FPU)
- **[Control Word](CONTROL8087.md)**:
  - 1330 = 0001 0011 0011 0000:
    -  1 Infinity Control Set
    - 00 round to nearest even
    - 11 64 bit precision
    - Masked exceptions
      - Precision
      - Underflow

```nasm
SYS:07A2 752D          JNZ	07D1
```

Exit if **[Control Word](CONTROL8087.md)** does not match expected setting, i.e. No FPU.

```nasm
SYS:07A4 54            PUSH	SP
SYS:07A5 58            POP	AX
SYS:07A6 3BC4          CMP	AX,SP
SYS:07A8 B001          MOV	AL,01
SYS:07AA 7525          JNZ	07D1
```

If stack behavior is different then:
- **AL** = 01 (8087)

```nasm
SYS:07AC 9B            WAIT
SYS:07AD DBE3          FINIT
```

Initialize FPU again.

```nasm
SYS:07AF 9B            WAIT
SYS:07B0 D9E8          FLD1
```

Load **1.0** onto the stack.

```nasm
SYS:07B2 9B            WAIT
SYS:07B3 D9EE          FLDZ
```

Load **0.0** onto the stack.

```nasm
SYS:07B5 9B            WAIT
SYS:07B6 DEF9          FDIVP	ST(1),ST
```

Divide **ST(1)** = **ST(1)** / **ST** = **1.0** / **0.0**, then pop off the stack.

```nasm
SYS:07B8 9B            WAIT
SYS:07B9 D9C0          FLD	ST(0)
```

Push **ST(0)** onto the stack.

```nasm
SYS:07BB 9B            WAIT
SYS:07BC D9E0          FCHS
```

Change the sign of **ST(0)**

```nasm
SYS:07BE 9B            WAIT
SYS:07BF DED9          FCOMPP	ST(1)
```

Compare **ST(0)** and **ST(1)**.

```nasm
SYS:07C1 9B            WAIT
SYS:07C2 DD3F          FSTSW	[BX]
```

Store **[Status Word](STATUS8087.md)** in [**BX**] (**[FPU.StatusWord]**).

```nasm
SYS:07C4 9B            WAIT
SYS:07C5 8B07          MOV	AX,[BX]
```

Copy **[Status Word](STATUS8087.md)** to **AX**.

```nasm
SYS:07C7 9E            SAHF
```

Store **AH** to **Flags**. At this point **AH** contains the upper-8 bits of the **[Status Word](STATUS8087.md)**:

|Bit|Name|Description      |Flag |
|---|----|-----------------| :-: |
|07 |B   |Busy             | SF  |
|06 |C3  |Condition Code 3 | ZF  |
|05 |ST  |Stack Top Pointer|  0  |
|04 |    |Stack Top Pointer| AF  |
|03 |    |Stack Top Pointer|  0  |
|02 |C2  |Condition Code 2 | PF  |
|01 |C1  |Condition Code 1 |  1  |
|00 |C0  |Condition Code 0 | CF  |

**SAHF** transfers its value into the Flags register using the mapping above.

```nasm
SYS:07C8 B002          MOV	AL,02
SYS:07CA 7405          JZ	07D1
```

If **ZF** is set (= 1) then FPU is a **80287** (**AL** = 02) because it does not distinguish between a postive infinity and a negative infinity.

```nasm
SYS:07CC BA3213        MOV	DX,1332
SYS:07CF B003          MOV	AL,03
```

Set:
- **AL** = 03 **FPU** is **80387**
- **[Control Word](CONTROL8087.md)**:
  - 1332 = 0001 0011 0011 0010:
    -  1 Infinity Control Set
    - 00 Round to nearest even
    - 11 64 bit precision
    - Masked exceptions
      - Precision
      - Underflow
      - Denormalized Operand

```nasm
SYS:07D1 A24D00        MOV	[Test8087],AL
SYS:07D4 8916A202      MOV	[FPU.ControlWord],DX
SYS:07D8 C3            RET
```

Set **[FPU type](../DATA.md)** and **[Control Word](DATA8087.md)** and return.

Because it returns with a **NEAR RET**, it can only be called from within the system library.

See also: [Test8087 (Variable)](../DATA.md), [8087/80287/80387 Control Word](CONTROL8087.md), [8087/80287/80387 Status Word](STATUS8087.md), [8087/80287/80387 x87 FPU Evironment](ENV8087.md), [8087/80287/80387 Temporary data](DATA8087.md), or go [back](../../README.md)
