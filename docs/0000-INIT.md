# 000 System Library Initialization

```nasm
SYS0000: MOV DX,DATA
SYS0003: MOV DS,DX
```

Point **DS** to System Library Data Segment.

```nasm
SYS0005: MOV [PrefixSeg],ES
```

Save PSP

```nasm
SYS0009: XOR BP,BP
```

??? Is this necessary?

```
SYS000B: CALL 0580
```

Clear input/output buffers and interrupt vectors array on call to **[SYS:0580 Clear Input/Output file records and Interrupt vector array](0580-CLEAR.md)**.

```nasm
SYS000E: CALL 00B1
```

Determine processor type on call to **[SYS:00B1 Test for 8086/80186/80286](00B1-TEST86.md)**.

```nasm
SYS0011: MOV AX,SP
SYS0013: ADD AX,0013
SYS0016: MOV CL,04
SYS0018: SHR AX,CL
```

Calculate segment address of the start of the **[overlay buffer](DATA.md)**. Start from **SP**, pad it (19 bytes), then get the size in terms of paragraphs by dividing **AX** by **16** by shifting it to the right via **CL** 4 times.

```nasm
SYS001A: MOV DX,SS
SYS001C: ADD AX,DX
```

Set segment address at a distance of **AX** away from the stack segment in **SS**.

```nasm
SYS001E: MOV [OvrHeapOrg.Segment],AX
SYS0021: MOV [OvrHeapPtr.Segment],AX
```

Set both segment address **[OvrHeapOrg](DATA.md)** (top of overlay buffer) and **[OvrHeapPtr](DATA.md)** (bottom of overlay buffer) to the segment address calculated in **AX**.

```nasm
SYS0024: ADD AX,[OvrHeapSize]
SYS0028: MOV [OvrHeapEnd],AX
```

Calculate the segment address of the end of the overlay buffer using **AX** (which currently points to the start of the overlay buffer) and adding the overlay buffer size (**[OvrHeapSize](DATA.md)**, in paragraphs) to it.

```nasm
SYS002B: MOV [HeapOrg.Segment],AX
SYS002E: MOV [HeapPtr.Segment],AX
SYS0031: MOV [FreeList.Segment],AX
```

Set the heap pointers (**[HeapOrg](DATA.md)**, **[HeapPtr](DATA.md)**) and the start of the free memory (**[FreeList](DATA.md)**) to the segment address of the end of the overlay buffer (**[OvrHeapEnd](DATA.md)**). 

```nasm
SYS0034: MOV ES,[PrefixSeg]
SYS0038: ES:
SYS0039: MOV AX,[PrefixSeg:0002]
```

Get length of image from **PSP:[0002]**.

```nasm
SYS003C: MOV [HeapEnd.Segment],AX
SYS003F: MOV WORD PTR [HeapError.Offset],00D6
SYS0045: MOV [HeapError.Segment],CS
```

Set up pointer to end of heap. Setup heap error exit routine at: **[SYS:00D6 Heap Error Handler](00D6-HEAP-ERROR.md)**

```nasm
SYS0049: MOV DI,SaveInt00
SYS004C: MOV SI,0239
```

Read list of interrupts in **[SYS:0239 Interrupt number list](0239-INTERRUPT-LIST.md)** and store them in array starting at **DATA**:[**SaveInt00**].

```nasm
SYS004F: MOV CX,0013
```

Get interrupt vectors for 19 interrupts.

```nasm
SYS0052: NOP
SYS0053: CLD
SYS0054: CS:
SYS0055: LODSB
SYS0056: MOV AH,35
SYS0058: INT 21
```

Use **DOS service INT 21h AH = 35h** to get interrupt vector. **AL** = interrupt number. Returns vector address in **ES**:**BX**

```nasm
SYS005A: MOV [DI],BX
SYS005C: MOV [DI+02],ES
SYS005F: ADD DI,+04
SYS0062: LOOP 0053
```

Store interrupt vector then read the next interrupt and adjust destination address.

```nasm
SYS0064: PUSH DS
```

Save **DS**

```nasm
SYS0065: PUSH CS
SYS0066: POP DS
```

Codes for handlers are in SYS code segment, so copy **CS** into **DS**.

```nasm
SYS0067: MOV DX,010C
SYS006A: MOV AX,2500
SYS006D: INT 21
```

Setup handler for INT 00h on **[SYS:010C Overflow Error Handler](010C-INT00H.md)**.

```nasm
SYS006F: MOV DX,0113
SYS0072: MOV AX,2523
SYS0075: INT 21
```

Setup handler for INT 23h on **[SYS:0113 Ctrl-C Handler](0113-CTRL-C-HANDLER.md)**.

```nasm
SYS0077: MOV DX,00DB
SYS007A: MOV AX,2524
SYS007D: INT 21
```

Setup handler for INT 24h on **[SYS:00DB Critical Error Handler](00DB-CRITICAL-ERROR.md)**.

```nasm
SYS007F: MOV DX,0104
SYS0082: MOV AX,253F
SYS0085: INT 21
```

Setup handler for INT 3Fh on **[SYS:0104 Overlay Manager Error Handler](0104-OVERMAN.md)**.

```nasm
SYS0087: POP DS
```

Restore **DS** to SYS DATA segment. Code now continues to **[0088 Initialize Input](0088-INIT-INPUT.md)**.

See also: [SYS:00B1 Test for 8086/80186/80286](00B1-TEST86.md), [SYS:0580 Clear Input/Output file records and Interrupt vector array](0580-CLEAR.md), [SYS:00D6 Heap Error Handler](00D6-HEAP-ERROR.md), [SYS:0239 Interrupt number list](0239-INTERRUPT-LIST.md), [SYS:010C Overflow Error Handler](010C-INT00H.md), [SYS:0113 Ctrl-C Handler](0113-CTRL-C-HANDLER.md),  [SYS:00DB Critical Error Handler](00DB-CRITICAL-ERROR.md), [SYS:0104 Overlay Manager Error Handler](0104-OVERMAN.md), or go [back](../README.md)