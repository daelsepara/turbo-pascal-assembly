# 000 System Library Initialization

```
SYS:0000 BACA07        MOV	DX,DATA
SYS:0003 8EDA          MOV	DS,DX
```

Point **DS** to System Library Data Segment.

```
SYS:0005 8C063800      MOV	[PrefixSeg],ES
```

Save PSP

```
SYS:0009 33ED          XOR	BP,BP
```

??? Is this necessary?

```
SYS:000B E87205        CALL	0580
```

Clear input/output buffers and interrupt vectors array on call to **[SYS:0580 Clear Input/Output file records and Interrupt vector array](0580-CLEAR.md)**.

```
SYS:000E E8A000        CALL	00B1
```

Determine processor type on call to **[SYS:00B1 Test for 8086/80186/80286](00B1-TEST86.md)**.

```
SYS:0011 8BC4          MOV	AX,SP
SYS:0013 051300        ADD	AX,0013
SYS:0016 B104          MOV	CL,04
SYS:0018 D3E8          SHR	AX,CL
```

Calculate segment address of the start of the **[overlay buffer](DATA.md)**. Start from **SP**, pad it (19 bytes), then get the size in terms of paragraphs by dividing **AX** by **16** by shifting it to the right via **CL** 4 times.

```
SYS:001A 8CD2          MOV	DX,SS
SYS:001C 03C2          ADD	AX,DX
```

Set segment address at a distance of **AX** away from the stack segment in **SS**.

```
SYS:001E A30A00        MOV	[OvrHeapOrg.Segment],AX
SYS:0021 A30C00        MOV	[OvrHeapPtr.Segment],AX
```

Set both segment address **[OvrHeapOrg](DATA.md)** (top of overlay buffer) and **[OvrHeapPtr](DATA.md)** (bottom of overlay buffer) to the segment address calculated in **AX**.

```
SYS:0024 03060400      ADD	AX,[OvrHeapSize]
SYS:0028 A30E00        MOV	[OvrHeapEnd],AX
```

Calculate the segment address of the end of the overlay buffer using **AX** (which currently points to the start of the overlay buffer) and adding the overlay buffer size (**[OvrHeapSize](DATA.md)**, in paragraphs) to it.

```
SYS:002B A31800        MOV	[HeapOrg.Segment],AX
SYS:002E A31C00        MOV	[HeapPtr.Segment],AX
SYS:0031 A32400        MOV	[FreeList.Segment],AX
```

Set the heap pointers (**[HeapOrg](DATA.md)**, **[HeapPtr](DATA.md)**) and the start of the free memory (**[FreeList](DATA.md)**) to the segment address of the end of the overlay buffer (**[OvrHeapEnd](DATA.md)**). 

```
SYS:0034 8E063800      MOV	ES,[PrefixSeg]
SYS:0038 26            ES:
SYS:0039 A10200        MOV	AX,[PrefixSeg:0002]
```

Get length of image from **PSP:[0002]**.

```
SYS:003C A32000        MOV	[HeapEnd.Segment],AX
SYS:003F C7062A00D600  MOV	WORD PTR [HeapError.Offset],SYS:00D6
SYS:0045 8C0E2C00      MOV	[HeapError.Segment],CS
```

Set up pointer to end of heap. Setup heap error exit routine at: **[SYS:00D6 Heap Error Handler](00D6-HEAP-ERROR.md)**

```
SYS:0049 BF5002        MOV	DI,SaveInt00
SYS:004C BE3902        MOV	SI,SYS:0239
```

Read list of interrupts in **[SYS:0239 Interrupt number list](0239-INTERRUPT-LIST.md)** and store them in array starting at **DATA**:[**SaveInt00**].

```
SYS:004F B91300        MOV	CX,0013
```

Get interrupt vectors for 19 interrupts.

```
SYS:0052 90            NOP
SYS:0053 FC            CLD
SYS:0054 2E            CS:
SYS:0055 AC            LODSB
SYS:0056 B435          MOV	AH,35
SYS:0058 CD21          INT	21
```

Use **DOS service INT 21h AH = 35h** to get interrupt vector. **AL** = interrupt number. Returns vector address in **ES**:**BX**

```
SYS:005A 891D          MOV	[DI],BX
SYS:005C 8C4502        MOV	[DI+02],ES
SYS:005F 83C704        ADD	DI,+04
SYS:0062 E2EF          LOOP	0053
```

Store interrupt vector then read the next interrupt and adjust destination address.

```
SYS:0064 1E            PUSH	DS
```

Save **DS**

```
SYS:0065 0E            PUSH	CS
SYS:0066 1F            POP	DS
```

Codes for handlers are in SYS code segment, so copy **CS** into **DS**.

```
SYS:0067 BA0C01        MOV	DX,010C
SYS:006A B80025        MOV	AX,2500
SYS:006D CD21          INT	21
```

Setup handler for INT 00h on **[SYS:010C Overflow Error Handler](010C-INT00H.md)**.

```
SYS:006F BA1301        MOV	DX,SYS:0113
SYS:0072 B82325        MOV	AX,2523
SYS:0075 CD21          INT	21
```

Setup handler for INT 23h on **[SYS:0113 Ctrl-C Handler](0113-CTRL-C-HANDLER.md)**.

```
SYS:0077 BADB00        MOV	DX,SYS:00DB
SYS:007A B82425        MOV	AX,2524
SYS:007D CD21          INT	21
```

Setup handler for INT 24h on **[SYS:00DB Critical Error Handler](00DB-CRITICAL-ERROR.md)**.

```
SYS:007F BA0401        MOV	DX,SYS:0104
SYS:0082 B83F25        MOV	AX,253F
SYS:0085 CD21          INT	21
```

Setup handler for INT 3Fh on **[SYS:0104 Overlay Manager Error Handler](0104-OVERMAN.md)**.

```
SYS:0087 1F            POP	DS
```

Restore **DS** to SYS DATA segment. Code now continues to **[0088 Initialize Input](0088-INIT-INPUT.md)**.

See also: [SYS:00B1 Test for 8086/80186/80286](00B1-TEST86.md), [SYS:0580 Clear Input/Output file records and Interrupt vector array](0580-CLEAR.md), [SYS:00D6 Heap Error Handler](00D6-HEAP-ERROR.md), [SYS:0239 Interrupt number list](0239-INTERRUPT-LIST.md), [SYS:010C Overflow Error Handler](010C-INT00H.md), [SYS:0113 Ctrl-C Handler](0113-CTRL-C-HANDLER.md),  [SYS:00DB Critical Error Handler](00DB-CRITICAL-ERROR.md), [SYS:0104 Overlay Manager Error Handler](0104-OVERMAN.md), or go [back](../README.md)